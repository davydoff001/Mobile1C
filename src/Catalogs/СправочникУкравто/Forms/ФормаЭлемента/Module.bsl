
&НаКлиенте
Процедура НабратьТелефон(Команда)
	ПозвонитьПоНомеру(Объект.Телефон);
КонецПроцедуры

&НаКлиенте
Процедура ПозвонитьПоНомеру(НомерТелефона)
	
	Если ПустаяСтрока(СокрЛП(НомерТелефона)) Тогда
		Возврат;
	КонецЕсли;
			
	#Если МобильноеПриложениеКлиент Тогда
	Если СредстваТелефонии.ПоддерживаетсяНаборНомера() Тогда
	    СредстваТелефонии.НабратьНомер(НомерТелефона, Истина);
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Функция ОбработатьТелефон(Телефон)
	Телефон = СтрЗаменить(Телефон,"-","");
	Телефон = СтрЗаменить(Телефон," ","");
	Телефон = СокрЛП(Телефон);
	
	Возврат Телефон;
КонецФункции

&НаКлиенте
Процедура НабратьТелефон1(Команда)
	ПозвонитьПоНомеру(Объект.Телефон1);
КонецПроцедуры

&НаКлиенте
Процедура НабратьТелефон2(Команда)
	ПозвонитьПоНомеру(Объект.Телефон2);
КонецПроцедуры

&НаКлиенте
Процедура НабратьТелефон3(Команда)
	ПозвонитьПоНомеру(Объект.Телефон3);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщениеНаПочту()
	Если ЗначениеЗаполнено(Объект.Почта) Тогда
		ТемаСообщения = "Введите тему сообщения!";
		ТекстСообщения = "Введите текст сообщения!";
		ЗапуститьПриложение("mailto:" + СокрЛП(Объект.Почта) + "?subject=" + ТемаСообщения + "&body=" + ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НаписатьНаПочту(Команда)
	ОтправитьСообщениеНаПочту();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКонтакт(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		ДанныеКонтакта = Новый ДанныеКонтакта;
		ЗаполнитьКонтакты(ДанныеКонтакта);
		
		МК = Новый МенеджерКонтактов;
				
		НовыйКонтакт = Новый ДанныеКонтактаУчетнойЗаписи(ДанныеКонтакта, МК.ПолучитьЛокальнуюУчетнуюЗаписьКонтактов()); 
		МК.ДобавитьКонтакт(НовыйКонтакт);
		Сообщить("Контакт "+СокрЛП(Объект.Наименование)+" успешно добавлен!");
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКонтакты(ДанныеКонтакта)
	#Если МобильноеПриложениеКлиент Тогда
		РеквизитыКонтактов = Новый Массив;
		РеквизитыКонтактов.Добавить("Организация");
		РеквизитыКонтактов.Добавить("Должность");
		Для каждого Стр Из РеквизитыКонтактов Цикл
			Если НЕ ЗначениеЗаполнено(Объект[Стр]) Тогда Продолжить КонецЕсли; 
			ДанныеКонтакта[Стр] = Объект[Стр];
		КонецЦикла;
		ДанныеКонтакта.Имя = СокрЛП(Объект.Наименование);
		ДанныеКонтакта.НомераТелефонов = ПолучитьМассивДанных("Телефон",ТипНомераТелефонаДанныхКонтакта);
		ДанныеКонтакта.АдресаЭлектроннойПочты = ПолучитьМассивДанных("Почта",ТипАдресаЭлектроннойПочтыДанныхКонтакта);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Функция ПолучитьМассивДанных(ИмяРеквизита,ТипДанныхОбъекта)
	Массив = Новый Массив;
	#Если МобильноеПриложениеКлиент Тогда
 
		ОписаниеКонтакта = Новый ЭлементДанныхКонтакта(ТипДанныхОбъекта["Рабочий"], Объект[ИмяРеквизита]);
		Массив.Добавить(ОписаниеКонтакта);
			
	#КонецЕсли
	Возврат Массив;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПроверимНаличиеКонтакта();
КонецПроцедуры

&НаКлиенте
Процедура ПроверимНаличиеКонтакта()
		
	#Если МобильноеПриложениеКлиент Тогда
		МК = Новый МенеджерКонтактов;
		Отбор = Новый ОтборКомпоновкиДанных;
		НовыйЭлемент = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НовыйЭлемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НомераТелефонов");
		НовыйЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		НовыйЭлемент.ПравоеЗначение = Объект.Телефон;
		НовыйЭлемент.Использование = Истина;
		КоличествоЗаписей=МК.НайтиКонтакты(Отбор);
		Если КоличествоЗаписей.Количество()>0 Тогда
			Элементы.ДекорацияКонтактСуществует.Заголовок="Контакт уже есть в телефонной книге";
			Элементы.ФормаСоздатьКонтакт.Видимость = Ложь;
		КонецЕсли; 
	#КонецЕсли
	
КонецПроцедуры



